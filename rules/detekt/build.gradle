// Copyright 2022 Twitter, Inc.
// SPDX-License-Identifier: Apache-2.0
plugins {
    id 'org.jetbrains.kotlin.jvm'
    alias(libs.plugins.shadowJar)
}
apply plugin: "com.vanniktech.maven.publish"

test {
    useJUnitPlatform()
}

// if publishing and it's not the uber jar, we want to remove the shadowRuntimeElements variant
if (!project.hasProperty("uberJar")) {
    AdhocComponentWithVariants javaComponent = (AdhocComponentWithVariants) project.components.findByName("java")
    javaComponent.withVariantsFromConfiguration(configurations["shadowRuntimeElements"]) {
        skip()
    }
}

dependencies {
    api libs.detekt.core
    api project(':rules:common')
    api project(':core-detekt')

    implementation project(':core-common')

    testImplementation libs.detekt.test
    testImplementation libs.junit5
    testImplementation libs.junit5.params
    testImplementation libs.assertj
}
